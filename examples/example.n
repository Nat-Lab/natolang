# a function declaration
fun get_num {
    # a variable declaration
    var c = getc(); 
    var n = 0;
    while (c != '\n') {
        n = n * 10 + c - '0';
        c = getc();
    }
    # the return value of a function will be the value of the last expression in 
    # the function.
    n; 
}

prints("input a number: ");
var n = get_num();
printi(n);
printc('\n');

fun fib {
    # although function automatically returns the last value, you can return 
    # manually.
    if ($1 <= 1) return $1;
    fib($1-1) + fib($1-2);
}

prints("fib(9)=");
printi(fib(9));
printc('\n');

# function can be nested.
fun fa {
    fun fb {
        $1 + 1;
    }
    fb($1) + 2;
}
printi(fa(1));
printc('\n');

# an array declaration. note that the content of this array is our VM's ASM 
# code.
var asm[20] = {
    40,         # SRS       SubRoutine Start
    2, 'H',     # IMM 'H'   IMMediate
    44,         # PAC       Print Accumulator Char
    2, 'e',     # IMM 'e'   IMMediate
    44,         # PAC       Print Accumulator Char
    2, 'l',     # IMM 'l'   IMMediate
    44,         # PAC       Print Accumulator Char
    2, 'l',     # IMM 'l'   IMMediate
    44,         # PAC       Print Accumulator Char
    2, 'o',     # IMM 'o'   IMMediate
    44,         # PAC       Print Accumulator Char
    2, '\n',    # IMM '\n'  IMMediate
    44,         # PAC       Print Accumulator Char
    39,         # SRE       SubRoutine End
};

# ;) so we can invoke the variable as a function.
asm(); # prints "Hello"

# arrays are just a normal variable, but with some extra spaces append after 
# them (size of the space specified with []). so you can consider all non-array 
# variables as an array of size one. this allow us to do some more fun (cursed) 
# stuffs.

var v1 = { 1, 2, 3 };
var v2;
var v3;
prints("v1: "); printi(v1); printc('\n'); # prints "1"
prints("v2: "); printi(v2); printc('\n'); # prints "2"
prints("v3: "); printi(v3); printc('\n'); # prints "3"
prints("v2[1]: "); printi(v2[1]); printc('\n'); # prints "3"

# what can you do with this you ask - well, take a look at this:
fun self_mod {
    printi(1);
    self_mod[2] = self_mod[2] + 1;
}

# self_mod function compiles to:
#   SRS,
#   IMM, 1,
#   PAI,
#   ...
#   SRE
# self_mod[2] is the operand of the IMM op. by changing self_mod[2] we changed 
# what will be IMM to the register next time. you can modify the function's 
# code in the same function! isn't that cool?

# every self_mod() call will increment the output by one.
while (self_mod() < 10);

printc('\n');

# set exit code.
0;
# and you can exit by exit();
exit();

